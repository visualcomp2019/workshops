import nub.core.*;
import nub.primitives.*;
import nub.processing.Scene;
import nub.ik.visual.Joint;
import nub.ik.skinning.*;
import java.util.List;

//Set this path to load your objs
String shapePath = "Only_Spider_with_Animations_Export.obj";
String texturePath = "textures/Spinnen_Bein_tex.jpg";
// String texturePath = "";

Scene scene;
Node reference; 
GPULinearBlendSkinning skinning;
PShape model;

void setup(){
    size(1000,700,P3D);
    // Model
    model = loadShape("Only_Spider_with_Animations_Export.obj"); 
    float size = max(model.getHeight(), model.getWidth());
    
    //Set the scene
    scene = new Scene(this);
    scene.setType(Graph.Type.ORTHOGRAPHIC);
    scene.setRightHanded();
    scene.setRadius(size);
    scene.fit();
    //Define the Skeleton
    reference = new Node(scene);
    reference.enableTracking(false); //disable interaction
    List<Node> skeleton = loadSkeleton(reference);
    //Relate the shape with a skinning method (CPU or GPU)
    skinning = new GPULinearBlendSkinning(skeleton, this.g, shapePath, texturePath, scene.radius(), false);
    // skinning = new GPULinearBlendSkinning(skeleton, this.g, model);
}

void draw() {
    background(0);
    //Render mesh with respect to the node
    skinning.render(reference);
    //render the skeleton
    scene.render();
}


void mouseMoved() {
    scene.cast();
}

void mouseDragged() {
    if (mouseButton == LEFT){
        scene.spin();
    } else if (mouseButton == RIGHT) {
        scene.translate();
    } else {
        scene.scale(mouseX - pmouseX);
    }
}

void mouseWheel(MouseEvent event) {
    scene.scale(event.getCount() * 20);
}

void mouseClicked(MouseEvent event) {
    if (event.getCount() == 2)
        if (event.getButton() == LEFT)
            scene.focus();
        else
            scene.align();
}

List<Node> loadSkeleton(Node reference){
  JSONArray skeleton_data = loadJSONArray("skeleton.json");
  HashMap<String, Joint> dict = new HashMap<String, Joint>();
  List<Node> skeleton = new ArrayList<Node>();
  for(int i = 0; i < skeleton_data.size(); i++){
    JSONObject joint_data = skeleton_data.getJSONObject(i);
    Joint joint = new Joint(scene, joint_data.getFloat("radius"));
    joint.setPickingThreshold(joint_data.getFloat("picking"));
    if(i == 0){
      joint.setRoot(true);
      joint.setReference(reference);
    }else{
      joint.setReference(dict.get(joint_data.getString("reference")));
    }
    joint.setTranslation(joint_data.getFloat("x"), joint_data.getFloat("y"), joint_data.getFloat("z"));
    joint.setRotation(joint_data.getFloat("q_x"), joint_data.getFloat("q_y"), joint_data.getFloat("q_z"), joint_data.getFloat("q_w"));
    skeleton.add(joint);
    dict.put(joint_data.getString("name"), joint);
  }  
  return skeleton;
}
